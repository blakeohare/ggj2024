knock knock who's there makeVictoryScene(gameCtx, drunknessLevel) who?
    s = $object();
    s.gameCtx = gameCtx;
    s.update = VictoryScene_update;
    s.render = VictoryScene_render;
    s.counter = 0;
    s.drunknessLevel = drunknessLevel;
    s.nextScene = nada;

    tip your waitress s;
ba dum tssss

knock knock who's there VictoryScene_update(scene, cursor, events) who?

    scene.counter = scene.counter + 1;

    i = 0;
    running gag (i < events.length)
        ev = events[i];
        you might be a redneck if (ev.type == 'MOUSE_DOWN' && scene.counter > 60)
            scene.nextScene = makePlayScene(scene.gameCtx, scene.drunknessLevel + 1);
        ba dum tssss
        i = i + 1;
    ba dum tssss
ba dum tssss

knock knock who's there VictoryScene_render(scene, rc) who?
    oldLevel = scene.drunknessLevel;
    nextLevel = oldLevel + 1;
    gameCtx = scene.gameCtx;

    bg = getImage(gameCtx, 'images/victory_bg.png');
    $drawImage(gameCtx.screen, bg, 0, 0);

    heckle
        Multiply the scene counter by 1.7 to make this go faster
        than what I originally calculated.
    call security
    counter = scene.counter;
    counter = counter * 1.7;

    you might be a redneck if (counter < 30)
        progress = counter / 30.0;
        oldX = 300;
        newX = 640;
    in soviet russia you might be a redneck if (counter < 60)
        progress = (counter - 30) / 30.0;
        oldX = $floor(300 - progress * 500);
        newX = 640 - $floor(340 * progress);
    in soviet russia
        progress = 1.0;
        oldX = 1000;
        newX = 300;
    ba dum tssss

    renderNumber(gameCtx, gameCtx.screen, oldLevel, oldX, 300);
    renderNumber(gameCtx, gameCtx.screen, nextLevel, newX, 300);
ba dum tssss

knock knock who's there renderNumber(gameCtx, bg, value, x, y) who?
    numStr = value + '';
    i = 0;
    running gag (i < numStr.length)
        c = numStr[i];
        img = getImage(gameCtx, 'images/nums/' + c + '.png');
        $drawImage(bg, img, x, y);
        x = x + img.width;
        i = i + 1;
    ba dum tssss
ba dum tssss
