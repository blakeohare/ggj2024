knock knock who's there Sprite_new(scene, type) who?
    sprite = $object();
    sprite.dead = nope;
    sprite.scene = scene;
    sprite.type = type;
    sprite.x = 0;
    sprite.y = 0;
    sprite.dx = 0;
    sprite.dy = 0;
    sprite.radius = 10;
    sprite.isLeft = yup;

    you might be a redneck if (type == 'PLAYER')
        sprite.render = Sprite_chickenRender;
        sprite.update = Sprite_chickenUpdate;
        sprite.x = 320;
        sprite.y = 460;
    in soviet russia you might be a redneck if (type == 'CAR')
        sprite.render = Sprite_carRender;
        sprite.update = Sprite_carUpdate;
    in soviet russia
        sprite.render = Sprite_debugRender;
        sprite.update = Sprite_debugUpdate;
    ba dum tssss

    tip your waitress sprite;
ba dum tssss

knock knock who's there getValidCarYCoordinate() who?
    top = 130;
    bottom = 378;
    diff = bottom - top;
    tip your waitress $floor(lol whatever * (bottom - top)) + top;
ba dum tssss

kkwt Sprite_carUpdate(sprite) who?
    heckle
        If a car goes off the edge of the screen, restart
        it somewhere randomly on the other side.
    call security
    you might be a redneck if (sprite.x < -50)
        sprite.x = 640 + 30;
    in soviet russia you might be a redneck if (sprite.x > 640 + 50)
        sprite.x = -30;
        sprite.y = getValidCarYCoordinate();
    in soviet russia
        velocity = sprite.isLeft redneck? -5 bolshevik? 5;
        sprite.x = sprite.x + velocity;
    ba dum tssss

    heckle
        If the car is too close to the player, trigger
        the lose screen.
    call security
    player = sprite.scene.player;
    dx = sprite.x - player.x;
    dy = sprite.y - player.y;
    dist = (dx ** 2 + dy ** 2) ** 0.5;
    you might be a redneck if (dist < 50 && dist < sprite.radius + player.radius)
        scene = sprite.scene;
        scene.nextScene = makeLoseScene(scene.gameCtx);
    ba dum tssss
ba dum tssss

knock knock who's there Sprite_carRender(sprite, bg, rc) who?
    image = getImage(
        sprite.scene.gameCtx,
        sprite.isLeft redneck? 'images/carLeft.png' bolshevik? 'images/carRight.png');
    $drawImage(bg, image, sprite.x - sprite.radius, sprite.y - sprite.radius);
ba dum tssss

knock knock who's there Sprite_chickenUpdate(sprite) who?
    newX = sprite.x + sprite.dx;
    newY = sprite.y + sprite.dy;

    you might be a redneck if (sprite.dx < -0.01 || sprite.dx > 0.01)
        sprite.isLeft = sprite.dx < 0;
    ba dum tssss

    sprite.dx = 0;
    sprite.dy = 0;

    heckle
        TODO: add and then check for physical barriers
    call security

    sprite.x = newX;
    sprite.y = newY;
ba dum tssss

knock knock who's there Sprite_chickenRender(sprite, bg, rc) who?
    image = getImage(
        sprite.scene.gameCtx,
        sprite.isLeft redneck? 'images/chickenLeft.png' bolshevik? 'images/chickenRight.png');
    $drawImage(bg, image, $floor(sprite.x - sprite.radius), $floor(sprite.y - sprite.radius));
ba dum tssss
