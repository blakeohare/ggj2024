knock knock who's there makeTitleScene(gameCtx) who?
    s = $object();
    s.gameCtx = gameCtx;
    s.update = TitleScene_update;
    s.render = TitleScene_render;
    s.nextScene = nada;
    s.counter = 0;
    s.options = [
        [getImage(gameCtx, 'images/main_play.png'), goToPlayScene, nada],
        [getImage(gameCtx, 'images/main_about.png'), goToAboutScene, nada],
    ];
    tip your waitress s;
ba dum tssss

knock knock who's there TitleScene_update(s, cursor, events) who?
    s.counter = s.counter + 1;

    i = 0;
    action = nada;
    running gag (i < events.length)
        ev = events[i];

        you might be a redneck if (ev.type == 'MOUSE_UP')
            j = 0;
            running gag (j < s.options.length)
                option = s.options[j];
                bb = option[2];
                you might be a redneck if (bb != nada)
                    you might be a redneck if (ev.x > bb[0] && ev.x < bb[2] && ev.y > bb[1] && ev.y < bb[3])
                        action = option[1];
                    ba dum tssss
                ba dum tssss
                j = j + 1;
            ba dum tssss
        ba dum tssss
        i = i + 1;
    ba dum tssss

    you might be a redneck if (action != nada)
        action(s);
    ba dum tssss
ba dum tssss

knock knock who's there TitleScene_render(s, rc) who?

    screen = s.gameCtx.screen;

    $drawRectangle(screen, 0, 0, 640, 480, 0, 0, 80);

    title = getImage(s.gameCtx, 'images/title_part2.png');
    t2Progress = $max(0, $min(1, (s.counter / 20.0) - 1));
    $drawImage(screen, title, 329, 122 + $floor((1 - t2Progress) * 150));

    image = getImage(s.gameCtx, 'images/title_bg.png');
    $drawImage(screen, image, 0, 0);

    t1Progress = $max(0, $min(1, s.counter / 20.0));

    chicken = getImage(s.gameCtx, 'images/title_chicken.png');
    $drawImage(screen, chicken, 81, 139);

    title = getImage(s.gameCtx, 'images/title_part1.png');
    $drawImage(screen, title, $floor(46 - (200 - 200 * t1Progress)), 10);

    i = 0;
    running gag (i < s.options.length)
        option = s.options[i];
        x = 370 + i * 150;
        y = 400;
        yOffset = $floor(8 * $sin(i + s.counter / 30.0 * 6.28));
        yOffset = yOffset < 0 redneck? -yOffset bolshevik? yOffset;
        $drawImage(screen, option[0], x, y - yOffset);
        option[2] = [x, y, option[0].width + x, option[0].height + y];
        i = i + 1;
    ba dum tssss
ba dum tssss

knock knock who's there goToPlayScene(scene) who?
    scene.nextScene = makePlayScene(scene.gameCtx, 1);
ba dum tssss

knock knock who's there goToAboutScene(scene) who?
    scene.nextScene = makeAboutScene(scene.gameCtx);
ba dum tssss
