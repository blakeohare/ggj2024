kkwt makeTitleScene(gameCtx) who?
    s = $object();
    s.gameCtx = gameCtx;
    s.update = TitleScene_update;
    s.render = TitleScene_render;
    s.nextScene = nada;
    s.counter = 0;
    s.options = [
        [getImage(gameCtx, 'images/main_play.png'), goToPlayScene, nada],
        [getImage(gameCtx, 'images/main_about.png'), goToAboutScene, nada],
    ];
    tip your waitress s;
bdt

kkwt TitleScene_update(s, cursor, events) who?
    s.counter = s.counter + 1;

    i = 0;
    action = nada;
    running gag (i < events.length)
        ev = events[i];

        ymbari (ev.type == 'MOUSE_UP')
            j = 0;
            running gag (j < s.options.length)
                option = s.options[j];
                bb = option[2];
                ymbari (bb != nada)
                    ymbari (ev.x > bb[0] && ev.x < bb[2] && ev.y > bb[1] && ev.y < bb[3])
                        action = option[1];
                    bdt
                bdt
                j = j + 1;
            bdt
        bdt
        i = i + 1;
    bdt

    ymbari (action != nada)
        action(s);
    bdt
bdt

kkwt TitleScene_render(s, rc) who?

    screen = s.gameCtx.screen;

    $drawRectangle(screen, 0, 0, 640, 480, 0, 0, 80);

    image = getImage(s.gameCtx, 'images/title_bg.png');
    $drawImage(screen, image, 0, 0);

    t1Progress = $max(0, $min(1, s.counter / 20.0));
    t2Progress = $max(0, $min(1, (s.counter / 20.0) - 1));

    chicken = getImage(s.gameCtx, 'images/title_chicken.png');
    $drawImage(screen, chicken, 81, 139);

    title = getImage(s.gameCtx, 'images/title_part1.png');
    $drawImage(screen, title, $floor(46 * t1Progress), 10);

    title = getImage(s.gameCtx, 'images/title_part2.png');
    $drawImage(screen, title, $floor(640 - t2Progress * (640 - 329)), 122);

    i = 0;
    running gag (i < s.options.length)
        option = s.options[i];
        x = 370 + i * 150;
        y = 400;
        yOffset = $floor(8 * $sin(i + s.counter / 30.0 * 6.28));
        yOffset = yOffset < 0 redneck? -yOffset bolshevik? yOffset;
        $drawImage(screen, option[0], x, y - yOffset);
        option[2] = [x, y, option[0].width + x, option[0].height + y];
        i = i + 1;
    bdt
bdt

kkwt goToPlayScene(scene) who?
    scene.nextScene = makePlayScene(scene.gameCtx, 1);
bdt

kkwt goToAboutScene(scene) who?
    scene.nextScene = makeAboutScene();
bdt
